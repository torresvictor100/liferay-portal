definition {

	macro getAllCompanyNames {
		if (!(isSet(portalInstanceName))) {
			var portalInstanceName = JSONCompany.getPortalInstanceNameNoSelenium();
		}

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var curl = '''
			${portalURL}/api/jsonws/company/get-companies \
				-u test@liferay.com:test
		''';

		var companies = JSONCurlUtil.get(${curl}, "$..['mx']");

		return ${companies};
	}

	macro getCompanyGroupIdByGroupKey {
		Variables.assertDefined(parameterList = "${groupKey},${portalInstanceName}");

		var companyId = JSONCompany.getCompanyId(portalInstanceName = ${portalInstanceName});

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups \
				-u test@liferay.com:test \
				-d companyId=${companyId} \
				-d parentGroupId=0 \
				-d site=true \
				-d start=0 \
				-d end=100
		''';

		var groupId = JSONCurlUtil.post(${curl}, "$.[?(@['groupKey'] == '${groupKey}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${groupId};
	}

	macro getCompanyId {
		if (!(isSet(portalInstanceName)) || (${portalInstanceName} == "liferay.com")) {
			var portalInstanceName = JSONCompany.getPortalInstanceName();
		}

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(creatorEmailAddress))) {
			if (${authenticationMethod} == "By Screen Name") {
				if (!(isSet(userScreenName))) {
					var creatorEmailAddress = "test";
				}
				else {
					var creatorEmailAddress = ${userScreenName};
				}
			}
			else {
				var creatorEmailAddress = "test@liferay.com";
			}
		}

		if (!(isSet(creatorPassword))) {
			var creatorPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/company/get-company-by-virtual-host/virtual-host/${portalInstanceName} \
				-u ${creatorEmailAddress}:${creatorPassword}
		''';

		var companyId = JSONCurlUtil.get(${curl}, "$['companyId']");

		return ${companyId};
	}

	macro getCompanyIdNoSelenium {
		if (!(isSet(portalInstanceName))) {
			var portalInstanceName = JSONCompany.getPortalInstanceNameNoSelenium();
		}

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(creatorEmailAddress))) {
			var creatorEmailAddress = "test@liferay.com";
		}

		if (!(isSet(creatorPassword))) {
			var creatorPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/company/get-company-by-virtual-host/virtual-host/${portalInstanceName} \
				-u ${creatorEmailAddress}:${creatorPassword}
		''';

		var companyId = JSONCurlUtil.get(${curl}, "$['companyId']");

		return ${companyId};
	}

	macro getDefaultPortalURL {
		var portalURL = PropsUtil.get("default.portal.url");

		if (!(contains(${portalURL}, "http"))) {
			var portalURL = "http://${portalURL}";
		}

		return ${portalURL};
	}

	macro getPortalInstanceName {
		var testNameSkipPortalInstance = PropsUtil.get("test.name.skip.portal.instance");
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ((${testPortalInstance} == "true") && !(IsTestName(locator1 = "", value1 = ${testNameSkipPortalInstance}))) {
			var portalURL = JSONCompany.getPortalURL();

			var portalInstanceName = JSONCompany.parsePortalInstanceURL(portalURL = ${portalURL});

			return ${portalInstanceName};
		}
		else {
			return "localhost";
		}
	}

	macro getPortalInstanceNameNoSelenium {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			var portalURL = JSONCompany.getPortalURL();

			var portalInstanceName = JSONCompany.parsePortalInstanceURL(portalURL = ${portalURL});

			return ${portalInstanceName};
		}
		else {
			return "localhost";
		}
	}

	macro getPortalURL {
		var portalURL = PropsUtil.get("portal.url");

		return ${portalURL};
	}

	macro parsePortalInstanceURL {
		Variables.assertDefined(parameterList = ${portalURL});

		var portalInstanceName = StringUtil.replace(${portalURL}, "http://", "");

		var portalInstanceName = StringUtil.replace(${portalInstanceName}, ":8080", "");

		return ${portalInstanceName};
	}

}