definition {

	macro activityCollapseButton {
		Click.mouseDownMouseUp(
			key_textButton = ${button},
			locator1 = "PRMMDFRequest#TEXT_BUTTON");
	}

	@summary = "Add a new activity"
	macro addNewActivity {
		Click(
			key_buttonText = "Add Activity",
			locator1 = "PRMMDFRequest#ADD_BUTTON");
	}

	@summary = "Assert claim message"
	macro assertClaimMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "PRMMDFRequest#CLAIM_MESSAGE",
			value1 = ${key_claimMessage});
	}

	@summary = "Cannot view Manager Status Button on MDF Request detail page"
	macro CanNotViewStateManager {
		AssertElementNotPresent(
			key_statusButton = "Approve",
			locator1 = "PRMMDFRequest#STATUS_BUTTON");
	}

	@summary = "Change MDF status"
	macro changeMDFStatus {
		ClickNoError(
			buttonId = ${key_statusButton},
			locator1 = "PRMMDFRequest#STATUS_BUTTON");

		Confirm.acceptAlert();
	}

	@summary = "Add a new MDF Request filling the goals and activity pages"
	macro createNewRequest {
		WaitForPageLoad();

		PRMMDFRequest.fillGoalsPage(
			accountName = ${accountName},
			additionalOptions = ${additionalOptions},
			campaignDescription = ${campaignDescription},
			campaignName = ${campaignName},
			countryName = ${countryName},
			goalsOptions = ${goalsOptions},
			targetMarketOptions = ${targetMarketOptions},
			targetRolesOptions = ${targetRolesOptions});

		Button.click(button = "Continue");

		PRMMDFRequest.newActivity(
			activityDescription = ${activityDescription},
			activityName = ${activityName},
			budgetList = ${budgetList},
			followUpStrategy = ${followUpStrategy},
			leadFollowUpList = ${leadFollowUpList},
			leadList = ${leadList},
			tactic = ${tactic},
			targetOfLeads = ${targetOfLeads},
			typeOfActivity = ${typeOfActivity});

		Button.click(button = "Continue");

		Button.click(button = "Continue");

		ScrollWebElementIntoView(locator1 = "Button#SUBMIT");

		Button.click(button = "Submit");

		WaitForPageLoad();

		Alert.viewSuccessMessage();
	}

	@summary = "Edit total MDF amount field"
	macro editTotalMDFValue {
		if (!(isSet(activitiyIndex))) {
			var activityIndex = 0;
		}

		Type(
			key_fieldName = "activities[${activityIndex}].mdfRequestAmount",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = ${newValue});

		KeyPress(
			key_fieldName = "activities[${activityIndex}].mdfRequestAmount",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "\TAB");
	}

	@summary = "Fill the goals page form"
	macro fillGoalsPage {
		if (isSet(accountName)) {
			Select(
				key_fieldName = "company",
				locator1 = "PRMMDFRequest#SELECT",
				value1 = ${accountName});
		}

		if (isSet(countryName)) {
			Select(
				key_fieldName = "country",
				locator1 = "PRMMDFRequest#SELECT",
				value1 = ${countryName});
		}

		if (isSet(campaignName)) {
			Type(
				key_fieldName = "overallCampaignName",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${campaignName});
		}

		if (isSet(campaignDescription)) {
			Type(
				key_fieldName = "overallCampaignDescription",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${campaignDescription});
		}

		if (isSet(goalsOptions)) {
			for (var goalsList : list ${goalsOptions}) {
				Click.clickAtNotVisible(
					key_fieldName = "liferayBusinessSalesGoals",
					key_optionValue = ${goalsList},
					locator1 = "PRMMDFRequest#CHECKBOX");
			}
		}

		if (isSet(targetMarketOptions)) {
			for (var targetMarketList : list ${targetMarketOptions}) {
				Click.clickAtNotVisible(
					key_fieldName = "targetMarkets",
					key_optionValue = ${targetMarketList},
					locator1 = "PRMMDFRequest#CHECKBOX");
			}
		}

		if (isSet(additionalOptions)) {
			for (var optionsList : list ${additionalOptions}) {
				Click.pauseClickAt(
					key_buttonText = ${optionsList},
					key_fieldName = "additionalOption",
					locator1 = "PRMMDFRequest#INPUT_BUTTON");
			}
		}

		if (isSet(targetRolesOptions)) {
			for (var targetRolesList : list ${targetRolesOptions}) {
				Click.clickAtNotVisible(
					key_fieldName = "targetAudienceRoles",
					key_optionValue = ${targetRolesList},
					locator1 = "PRMMDFRequest#CHECKBOX");
			}
		}
	}

	@summary = "Format the date that will be inputed on start and end date"
	macro formatDate {
		var nextDayDate = DateUtil.getFormattedDate("+1", "MMddyyyy");

		if (isSet(endDate)) {
			var nextDayDate = DateUtil.getFormattedDate("+2", "MMddyyyy");
		}

		return ${nextDayDate};
	}

	@summary = "Go to MDF"
	macro goToMDF {
		Click(
			key_campaignName = ${campaignName},
			locator1 = "PRMMDFRequest#REQUEST_ID");
	}

	@summary = "Fill the date fields on Activity Form"
	macro insertDate {
		if (!(isSet(activityIndex))) {
			var activityIndex = 0;
		}

		var currentDate = DateUtil.getFormattedDate(${dateRange}, ${dateFormat});

		if (${fieldName} == "startDate") {
			Type.sendKeys(
				key_fieldName = "activities[${activityIndex}].startDate",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${currentDate});

			Click(
				key_fieldName = "activities[${activityIndex}].endDate",
				locator1 = "PRMMDFRequest#TEXT_FIELD");
		}

		if (${fieldName} == "endDate") {
			Type.sendKeys(
				key_fieldName = "activities[${activityIndex}].endDate",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${currentDate});

			Click(
				key_fieldName = "activities[${activityIndex}].startDate",
				locator1 = "PRMMDFRequest#TEXT_FIELD");
		}
	}

	@summary = "Add a new activity"
	macro newActivity {
		if (!(isSet(activityIndex))) {
			var activityIndex = 0;
		}

		Click(
			key_buttonText = "Add Activity",
			locator1 = "PRMMDFRequest#ADD_BUTTON");

		Type(
			key_fieldName = "activities[${activityIndex}].name",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = ${activityName});

		Select(
			key_fieldName = "activities[${activityIndex}].typeActivity",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = ${typeOfActivity});

		Select(
			key_fieldName = "activities[${activityIndex}].tactic",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = ${tactic});

		Type(
			key_fieldName = "activities[${activityIndex}].activityDescription.marketingActivity",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = ${activityDescription});

		Click.clickAtNotVisible(
			key_buttonText = ${leadList},
			key_fieldName = "activities[${activityIndex}].activityDescription.leadGenerated",
			locator1 = "PRMMDFRequest#INPUT_BUTTON");

		if (${leadList} == "Yes") {
			Type(
				key_fieldName = "activities[${activityIndex}].activityDescription.targetOfLeads",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${targetOfLeads});

			for (var leadFollowUp : list ${leadFollowUpList}) {
				Click.clickAtNotVisible(
					key_fieldName = "activities[${activityIndex}].activityDescription.leadFollowUpStrategies",
					key_optionValue = ${leadFollowUp},
					locator1 = "PRMMDFRequest#CHECKBOX");
			}

			Type(
				key_fieldName = "activities[${activityIndex}].activityDescription.detailsLeadFollowUp",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${followUpStrategy});
		}

		var startDate = PRMMDFRequest.formatDate();

		Type.sendKeys(
			key_fieldName = "activities[${activityIndex}].startDate",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = ${startDate});

		var endDate = PRMMDFRequest.formatDate(endDate = "true");

		Type.sendKeys(
			key_fieldName = "activities[${activityIndex}].endDate",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = ${endDate});

		var budgetIndex = 0;

		for (var bugdetValues : list ${budgetList}) {
			var expenseOption = StringUtil.extractFirst(${bugdetValues}, "-");
			var bugdetValue = StringUtil.extractLast(${bugdetValues}, "-");

			Click(
				key_buttonText = "Add Expense",
				locator1 = "PRMMDFRequest#ADD_BUTTON");

			Select(
				key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].expense",
				locator1 = "PRMMDFRequest#SELECT",
				value1 = ${expenseOption});

			Type(
				key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].cost",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = ${bugdetValue});

			var budgetIndex = ${budgetIndex} + 1;
		}
	}

	@summary = "Assert button is not clickable"
	macro viewDisabledButton {
		AssertElementPresent(
			key_text = ${buttonName},
			locator1 = "Button#ANY_DISABLED");
	}

	@summary = "Verify error messages"
	macro viewErrorMessage {
		var key_errorMessage = ${errorMessage};

		AssertElementPresent(locator1 = "PRMMDFRequest#ERROR_MESSAGE");
	}

	@summary = "Verify that a content is present in the MDF file"
	macro viewExportedMDF {
		for (var fileContentItem : list ${fileContentList}) {
			var directoryPath = PropsUtil.get("output.dir.name");

			var fileContent = FileUtil.read("${directoryPath}/${fileName}");

			if (!(contains(${fileContent}, ${fileContentItem}))) {
				echo(${fileContent});

				fail("The string '${fileContentItem}' doesn't exists on this file");
			}
			else {
				echo("The string '${fileContentItem}' exists on this file");
			}
		}
	}

	@summary = "Verify the fills done by the user on the Campaign Information"
	macro viewMDFActivityDetails {
		Click(
			key_textButton = "Activity Name 1",
			locator1 = "PRMMDFRequest#TEXT_BUTTON");

		AssertElementPresent(
			key_fieldName = "Activity name",
			key_nameValue = ${activityName},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Type of Activity",
			key_nameValue = ${typeOfActivity},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Tactic",
			key_nameValue = ${tactic},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Marketing activity",
			key_nameValue = ${activityDescription},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertTextEquals.assertPartialText(
			key_fieldName = "Start Date",
			key_nameValue = ${startDate},
			locator1 = "PRMMDFRequest#DETAIL_FIELD",
			value1 = ${startDate});

		AssertTextEquals.assertPartialText(
			key_fieldName = "End Date",
			key_nameValue = ${endDate},
			locator1 = "PRMMDFRequest#DETAIL_FIELD",
			value1 = ${endDate});

		AssertElementPresent(
			key_fieldName = ${expense},
			key_nameValue = ${budget},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Is a lead list an outcome of this activity?",
			key_nameValue = ${leadList},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");
	}

	@summary = "Verify the activity fills made by the user after submited"
	macro viewMDFCompanyDetails {
		AssertElementPresent(
			key_fieldName = "Company Name",
			key_nameValue = ${accountName},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Region",
			key_nameValue = ${countryName},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Name of the overall campaign",
			key_nameValue = ${campaignName},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Short description of the overall campaign",
			key_nameValue = "Campaign Description",
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Liferay business/sales goals",
			key_nameValue = ${goalsOptions},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Target Market(s)",
			key_nameValue = ${targetMarketOptions},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");

		AssertElementPresent(
			key_fieldName = "Target Audience/Role",
			key_nameValue = ${targetRolesOptions},
			locator1 = "PRMMDFRequest#DETAIL_FIELD");
	}

	@summary = "View MDF actual Status on MDF Request detail page "
	macro viewMDFStatus {
		AssertElementPresent(
			key_value = ${key_status},
			locator1 = "PRMMDFRequest#STATUS_INFO");
	}

	@summary = "Verify that the state manager is present on MDF Request detail page"
	macro viewStateManager {
		AssertElementPresent(
			key_statusButton = "Approve",
			locator1 = "PRMMDFRequest#STATUS_BUTTON");
	}

}